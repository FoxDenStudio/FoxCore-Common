apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            credentials {
                username = System.env.NEXUS_USER
                password = System.env.NEXUS_PASS
            }
            name = 'FDSSnapshots'
            url = 'http://repo.foxdenstudio.net/repository/maven-snapshots/'
        }
        maven {
            credentials {
                username = System.env.NEXUS_USER
                password = System.env.NEXUS_PASS
            }
            name = 'FDSReleases'
            url = 'http://repo.foxdenstudio.net/repository/maven-releases/'
        }
        maven {
            credentials {
                username = System.env.NEXUS_USER
                password = System.env.NEXUS_PASS
            }
            name = 'FDSDownloads'
            url = 'http://repo.foxdenstudio.net/repository/downloads/'
        }
    }
}

task travisPublish {
    group = 'publishing'
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java

            artifactId = archivesBaseName
            version = repoVersion

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
}

if (System.env.TRAVIS && !Boolean.valueOf(System.env.TRAVIS_PULL_REQUEST)) {
    if (System.env.TRAVIS_TAG) {
        travisPublish.dependsOn 'publishLibraryPublicationToFDSReleasesRepository'
    } else {
        travisPublish.dependsOn 'publishLibraryPublicationToFDSSnapshotsRepository'
    }
    //travisPublish.dependsOn 'publishDownloadPublicationToFDSDownloadsRepository'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}